#
# Macros and functions file for CMakeLists.txt files
#
# Author : Jean-Christophe FABRE <fabrejc@supagro.inra.fr>
#
# This file is included by the main CMakeLists.txt file, and defines macros
# and functions that are used in CMakeLists.txt files 
#



# Macro for compiling a simulation function
# FUNC_NAME : the name of the simulation function (without extension)
# FUNC_DIR : the sub-directory of the simulation function
# ARGN : install targets
MACRO(ADD_FUNCTION FUNC_NAME FUNC_SRCDIR FUNC_BINDIR)

  FILE(GLOB FUNC_CPP ${FUNC_SRCDIR}/*.cpp)
  ADD_LIBRARY(${FUNC_NAME} MODULE ${FUNC_CPP})
  SET_TARGET_PROPERTIES(${FUNC_NAME} PROPERTIES 
                        PREFIX ""
                        SUFFIX "${FUNCTIONS_BINARY_EXTENSION}"
                        LIBRARY_OUTPUT_DIRECTORY "${FUNC_BINDIR}")
                                                
  TARGET_LINK_LIBRARIES(${FUNC_NAME} openfluid-core openfluid-base openfluid-tools)
      
ENDMACRO(ADD_FUNCTION)



# Macro for compiling a builder extension
MACRO(ADD_BUILDER_EXTENSION EXT_NAME EXT_SRCDIR EXT_BINDIR)

  FILE(GLOB EXT_CPP ${EXT_SRCDIR}/*.cpp)
  ADD_LIBRARY(${EXT_NAME} MODULE ${EXT_CPP})
  SET_TARGET_PROPERTIES(${EXT_NAME} PROPERTIES 
                        PREFIX "" 
                        SUFFIX "${BUILDEREXTENSION_BINARY_EXTENSION}"
                        LIBRARY_OUTPUT_DIRECTORY "${EXT_BINDIR}")
                                                
  TARGET_LINK_LIBRARIES(${EXT_NAME} 
                        openfluid-core
                        openfluid-base 
                        openfluid-tools
                        ${GTKMM_LIBRARIES})
    
ENDMACRO(ADD_BUILDER_EXTENSION)




MACRO(COMPILE_PDFLATEX_DOC DOC_NAME DOC_BUILDDIR DOC_OUTDIR DOC_INSTALL_PATH)
  
  ADD_CUSTOM_COMMAND(
    OUTPUT  "${DOC_BUILDDIR}/${DOC_NAME}.pdf"
    DEPENDS "${DOC_BUILDDIR}"
    DEPENDS "${DOC_BUILDDIR}/${DOC_NAME}.tex"           
    COMMAND ${CMAKE_COMMAND} -E chdir ${DOC_BUILDDIR} ${PDFLATEX_COMPILER}
    ARGS "${DOC_BUILDDIR}/${DOC_NAME}.tex"       
    COMMAND ${CMAKE_COMMAND} -E chdir ${DOC_BUILDDIR} ${PDFLATEX_COMPILER}
    ARGS "${DOC_BUILDDIR}/${DOC_NAME}.tex"    
    )  


  ADD_CUSTOM_COMMAND(    
    OUTPUT  "${DOC_OUTDIR}/${DOC_NAME}.pdf"
    DEPENDS "${DOC_BUILDDIR}/${DOC_NAME}.pdf"      
    COMMAND ${CMAKE_COMMAND} -E copy ${DOC_BUILDDIR}/${DOC_NAME}.pdf ${DOC_OUTDIR}/${DOC_NAME}.pdf        
    )  


  ADD_CUSTOM_TARGET(${DOC_NAME}-doc-pdf ALL
    DEPENDS ${DOC_OUTDIR}/${DOC_NAME}.pdf
   )
 
 
  INSTALL(FILES ${DOC_OUTDIR}/${DOC_NAME}.pdf 
          DESTINATION ${DOC_INSTALL_PATH})
 
ENDMACRO(COMPILE_PDFLATEX_DOC DOC_NAME DOC_BUILDDIR DOC_OUTDIR DOC_INSTALL_PATH)





MACRO(COMPILE_LATEX2HTML_DOC DOC_NAME DOC_BUILDDIR DOC_OUTDIR DOC_INSTALL_PATH)

  ADD_CUSTOM_COMMAND(
    OUTPUT  "${DOC_BUILDDIR}/html"        
    DEPENDS "${DOC_BUILDDIR}"
    COMMAND ${CMAKE_COMMAND} -E make_directory ${DOC_BUILDDIR}/html       
   )  
    

  ADD_CUSTOM_COMMAND(
    OUTPUT  "${DOC_BUILDDIR}/html/${DOC_NAME}/index.html"
    DEPENDS "${DOC_BUILDDIR}/${DOC_NAME}.tex"
    DEPENDS "${DOC_BUILDDIR}/html"    
    COMMAND ${CMAKE_COMMAND} -E chdir ${DOC_BUILDDIR} ${LATEX2HTML_CONVERTER}
    ARGS -mkdir -split +1 -html_version 4.0,latin1,unicode -show_section_numbers -noinfo -local_icons -dir ${DOC_BUILDDIR}/html/${DOC_NAME} ${DOC_BUILDDIR}/${DOC_NAME}.tex       
    )  


  ADD_CUSTOM_COMMAND(    
    OUTPUT  "${DOC_OUTDIR}/${DOC_NAME}/"
    DEPENDS  "${DOC_BUILDDIR}/html/${DOC_NAME}/index.html"      
    COMMAND ${CMAKE_COMMAND} -E copy_directory ${DOC_BUILDDIR}/html/${DOC_NAME}/ ${DOC_OUTDIR}/${DOC_NAME}        
    )  


  ADD_CUSTOM_TARGET(${DOC_NAME}-doc-html ALL
    DEPENDS "${DOC_OUTDIR}/${DOC_NAME}/"
   )

ENDMACRO(COMPILE_LATEX2HTML_DOC DOC_NAME DOC_BUILDDIR DOC_OUTDIR DOC_INSTALL_PATH)

