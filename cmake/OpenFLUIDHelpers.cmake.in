##
#
#  This file is part of OpenFLUID software
#  Copyright(c) 2007, INRA - Montpellier SupAgro
#
#
# == GNU General Public License Usage ==
#
#  OpenFLUID is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  OpenFLUID is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with OpenFLUID. If not, see <http://www.gnu.org/licenses/>.
#
#
# == Other Usage ==
#
#  Other Usage means a use of OpenFLUID that is inconsistent with the GPL
#  license, and requires a written agreement between You and INRA.
#  Licensees for Other Usage of OpenFLUID may use this file in accordance
#  with the terms contained in the written agreement between You and INRA.
#  
##


CMAKE_MINIMUM_REQUIRED(VERSION 3.10)  

ENABLE_TESTING()


###########################################################################


FUNCTION(OPENFLUID_SHOW_CMAKE_VARIABLES)
  GET_CMAKE_PROPERTY(_VARNAMES VARIABLES)
  FOREACH(_VARNAME ${_VARNAMES})
    
    STRING(LENGTH ${_VARNAME} _VARNAMELEN)    
    IF(_VARNAMELEN GREATER 9)
      STRING(SUBSTRING ${_VARNAME} 0 10 _MATCHEDVARNAME)
      IF(_MATCHEDVARNAME STREQUAL "OpenFLUID_")
        MESSAGE("${_VARNAME} = ${${_VARNAME}}")
      ENDIF()
    ENDIF()
    
  ENDFOREACH()
ENDFUNCTION()


###########################################################################


MACRO(OPENFLUID_DETECT_CXX17)
  SET(CMAKE_CXX_STANDARD @OFBUILD_CXX_STANDARD@)
  SET(CMAKE_CXX_STANDARD_REQUIRED ON)
  SET(CMAKE_CXX_EXTENSIONS OFF)
ENDMACRO()


# Macro for CXX11 compatibility - deprecated
MACRO(OPENFLUID_DETECT_CXX14)
  MESSAGE(WARNING "OPENFLUID_DETECT_CXX14() is deprecated, use OPENFLUID_DETECT_CXX17() instead")
  OPENFLUID_DETECT_CXX17()
ENDMACRO()


# Macro for CXX11 compatibility - deprecated
MACRO(OPENFLUID_DETECT_CXX11)
  MESSAGE(WARNING "OPENFLUID_DETECT_CXX11() is deprecated, use OPENFLUID_DETECT_CXX17() instead")
  OPENFLUID_DETECT_CXX17()
ENDMACRO()

###########################################################################


# Macro for Qt detection - deprecated
MACRO(OPENFLUID_FIND_QT)
  MESSAGE(WARNING "The OPENFLUID_FIND_QT() macro is fully functional but deprecated."
                  "See file ${OpenFLUID_DIR}/OpenFLUIDDetectQt.cmake for more informations.") 
  INCLUDE(${OpenFLUID_DIR}/OpenFLUIDDetectQt.cmake)
ENDMACRO()


###########################################################################


# Macro for checking and displaying GDAL version
MACRO(OPENFLUID_CHECK_GDAL_VERSION)
  
  IF(NOT GDAL_INCLUDE_DIR)
    MESSAGE(FATAL_ERROR "Error checking GDAL version: GDAL_INCLUDE_DIR variable is not set")
  ENDIF()
  
  FILE(STRINGS "${GDAL_INCLUDE_DIR}/gdal_version.h" _GDAL_VERSION_CONFIG_LINE REGEX "#[\ ]*define[\ ]+GDAL_RELEASE_NAME")

  STRING(REGEX MATCH "[0-9]+.[0-9]+.[0-9]+(~[a-z]+[0-9]*)?" GDAL_VERSION "${_GDAL_VERSION_CONFIG_LINE}")

  MESSAGE(STATUS "GDAL version : ${GDAL_VERSION}")

ENDMACRO()



###########################################################################


FUNCTION(OPENFLUID_ADD_TEST)

  SET(_ONEARGS_CMDS NAME)
  SET(_MANYARGS_CMDS COMMAND PRE_TEST POST_TEST)
  
  SET(_ADD_TEST)
  
  CMAKE_PARSE_ARGUMENTS(_ADD_TEST "" "${_ONEARGS_CMDS}" "${_MANYARGS_CMDS}" ${ARGN})
  
  ADD_TEST(NAME ${_ADD_TEST_NAME} COMMAND "${CMAKE_COMMAND}" "-E" "chdir" "${CMAKE_CURRENT_BINARY_DIR}"
                                          "${CMAKE_COMMAND}" 
                                          "-DCMD=${_ADD_TEST_COMMAND}"
                                          "-DPRECMDS=${_ADD_TEST_PRE_TEST}"
                                          "-DPOSTCMDS=${_ADD_TEST_POST_TEST}"
                                          "-P" "${OpenFLUID_DIR}/OpenFLUIDTestScript.cmake")
  
  # add env vars for tests if present
  
  GET_TEST_PROPERTY(${_ADD_TEST_NAME} ENVIRONMENT _TEST_ENVVARS)
  
  IF (DEFINED OPENFLUID_TESTS_TEMP_PATH)
    IF(_TEST_ENVVARS)
      SET(_TEST_ENVVARS "${_TEST_ENVVARS}\;OPENFLUID_TEMP_PATH=${OPENFLUID_TESTS_TEMP_PATH}")
    ELSE()
      SET(_TEST_ENVVARS "OPENFLUID_TEMP_PATH=${OPENFLUID_TESTS_TEMP_PATH}")
    ENDIF()      
  ENDIF()

  IF (DEFINED OPENFLUID_TESTS_USERDATA_PATH)
    IF(_TEST_ENVVARS)
      SET(_TEST_ENVVARS "${_TEST_ENVVARS}\;OPENFLUID_USERDATA_PATH=${OPENFLUID_TESTS_USERDATA_PATH}")
    ELSE()
      SET(_TEST_ENVVARS "OPENFLUID_USERDATA_PATH=${OPENFLUID_TESTS_USERDATA_PATH}")
    ENDIF()      
  ENDIF()

  SET_TESTS_PROPERTIES(${_ADD_TEST_NAME} 
                       PROPERTIES ENVIRONMENT ${_TEST_ENVVARS})

ENDFUNCTION()


###########################################################################


FUNCTION(_OPENFLUID_WAREPLUGIN_BUILD)

  SET(_OPNFLD_OPENFLUID_COMPONENTS core base ware )
  SET(_OPNFLD_QT_REQUIRED OFF)
  SET(_OPNFLD_QT_WARE_UI)
  SET(_OPNFLD_QT_WARE_RC)
  SET(_OPNFLD_CPPSIGN_FILE @OPENFLUID_WARESDEV_BUILD_MAINSIGN@)
  SET(_OPNFLD_CMAKEINFO_FILE @OPENFLUID_WARESDEV_BUILD_MAININFO@)


  IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE "Debug")
  ENDIF()

  IF (NOT _FORCE_PARAMSUI)
    EXECUTE_PROCESS(COMMAND "${OpenFLUID_CMD_PROGRAM}" "info2build"
                            "--src-path=${CMAKE_SOURCE_DIR}" "--dest-path=${CMAKE_BINARY_DIR}"
                    RESULT_VARIABLE _WAREINFO_RESULT
                    OUTPUT_VARIABLE _WAREINFO_OUTPUT)
    IF(_WAREINFO_RESULT AND NOT _WAREINFO_RESULT EQUAL 0)
      MESSAGE(FATAL_ERROR "Error processing @OPENFLUID_WARESDEV_WAREINFO_FILE@ file" "\n" ${_WAREINFO_OUTPUT})
    ENDIF()
  ENDIF()

  IF (_FORCE_PARAMSUI)
    SET(_OPNFLD_CPPSIGN_FILE @OPENFLUID_WARESDEV_BUILD_PARAMSUISIGN@)
    SET(_OPNFLD_CMAKEINFO_FILE @OPENFLUID_WARESDEV_BUILD_PARAMSUIINFO@)
  ENDIF()

  INCLUDE("${CMAKE_BINARY_DIR}/${_OPNFLD_CMAKEINFO_FILE}")


  IF(WARE_IS_SIMULATOR)
    SET(_OPNFLD_WARE_BINSUBDIR @OPENFLUID_WARESBIN_PATH@/@OPENFLUID_SIMULATORS_PATH@)
  ELSEIF(WARE_IS_OBSERVER)
    SET(_OPNFLD_WARE_BINSUBDIR @OPENFLUID_WARESBIN_PATH@/@OPENFLUID_OBSERVERS_PATH@)
  ELSEIF(WARE_IS_BUILDEREXT)
    SET(_OPNFLD_WARE_BINSUBDIR @OPENFLUID_WARESBIN_PATH@/@OPENFLUID_BUILDEREXTS_PATH@)
  ELSE()
    MESSAGE(FATAL_ERROR "Wrong ware type")
  ENDIF()


  IF(WARE_IS_BUILDEREXT)
    SET(_OPNFLD_QT_REQUIRED ON)
  ENDIF()

  IF(WARE_IS_BUILDEREXT OR _BUILDARGS_WITH_PARAMSUI)
    SET(_OPNFLD_OPENFLUID_COMPONENTS ${_OPNFLD_OPENFLUID_COMPONENTS} ui-builderext)
  ENDIF()

  SET(_OPNFLD_OPENFLUID_COMPONENTS ${_OPNFLD_OPENFLUID_COMPONENTS} ${_BUILDARGS_OPENFLUID_COMPONENTS})

  OPENFLUID_DETECT_CXX17()

  IF(_OPNFLD_QT_REQUIRED)
    FIND_PACKAGE(Qt5 COMPONENTS Core Widgets REQUIRED)
    SET(CMAKE_INCLUDE_CURRENT_DIR ON)
  ENDIF()


  FIND_PACKAGE(OpenFLUID REQUIRED ${_OPNFLD_OPENFLUID_COMPONENTS})

  IF(WARE_IS_SIMULATOR)    
    SET(_OPNFLD_WARE_FILENAME_SUFFIX ${OpenFLUID_SIMULATOR_FILENAME_SUFFIX})
  ELSEIF(WARE_IS_OBSERVER)
    SET(_OPNFLD_WARE_FILENAME_SUFFIX ${OpenFLUID_OBSERVER_FILENAME_SUFFIX})
  ELSEIF(WARE_IS_BUILDEREXT)
    SET(_OPNFLD_WARE_FILENAME_SUFFIX ${OpenFLUID_BUILDEREXT_FILENAME_SUFFIX})
  ENDIF()


  SET(_OPNFLD_USERDATA_PATH "$ENV{HOME}/.@OPENFLUID_RELATIVE_PATH@")
  IF(WIN32)
    SET(_OPNFLD_USERDATA_PATH "$ENV{USERPROFILE}/@OPENFLUID_RELATIVE_PATH@") 
  ENDIF()

  IF(DEFINED ENV{OPENFLUID_USERDATA_PATH})
    SET(_OPNFLD_USERDATA_PATH "$ENV{OPENFLUID_USERDATA_PATH}")
  ENDIF()

  IF(NOT _BUILDARGS_INSTALL_PATH)
    SET(_OPNFLD_INSTALL_PATH "${_OPNFLD_USERDATA_PATH}/${_OPNFLD_WARE_BINSUBDIR}")
  ELSE()
    SET(_OPNFLD_INSTALL_PATH "${_BUILDARGS_INSTALL_PATH}")
  ENDIF()        

  SET(_ADDING_TEXT "${WARE_TYPE} ${WARE_ID}")
  IF(_FORCE_PARAMSUI)
    SET(_ADDING_TEXT "parameterization UI")
  ENDIF()
  MESSAGE(STATUS "Adding ${_ADDING_TEXT}")
  MESSAGE(STATUS "  Mode: ${CMAKE_BUILD_TYPE}")


  IF(WARE_IS_BUILDEREXT)
    QT5_WRAP_UI(_OPNFLD_QT_WARE_UI ${_BUILDARGS_UI_FILES})
    QT5_ADD_RESOURCES(_OPNFLD_QT_WARE_RC ${_BUILDARGS_RC_FILES})
  ENDIF()


  IF(NOT WARE_SOURCE_DIR)
    SET(WARE_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
  ENDIF()  
  
  
  
  SET(_WARE_TARGET ${_BUILDARGS_TARGET})
  IF (NOT _WARE_TARGET)
    SET(_WARE_TARGET ${WARE_ID})
  ENDIF()

  IF(_BUILDARGS_FORTRAN_FILES)
    ENABLE_LANGUAGE(Fortran)
    SET(CMAKE_Fortran_FLAGS_RELEASE "-funroll-all-loops -fno-f2c -O3")
    SET(_FORTRAN_LINK_LIBS "gfortran")
  ENDIF()


  SET(_OPNFLD_SRC_FILES ${_BUILDARGS_CPP_FILES} ${_BUILDARGS_FORTRAN_FILES})
  SET(_OPNFLD_UIRC_FILES ${_BUILDARGS_UI_FILES} ${_BUILDARGS_RC_FILES})

  SET(_TEMP_SRC_STR)
  FOREACH(_SRC_FILE ${_OPNFLD_SRC_FILES})
    SET(_TEMP_SRC_STR "${_TEMP_SRC_STR}${_SRC_FILE} ")
  ENDFOREACH()
  MESSAGE(STATUS "  Sources files: ${_TEMP_SRC_STR}")
  
  IF (_OPNFLD_UIRC_FILES)
    SET(_TEMP_UIRC_STR)
    FOREACH(_UIRC_FILE ${_OPNFLD_UIRC_FILES})
      SET(_TEMP_UIRC_STR "${_TEMP_UIRC_STR}${_UIRC_FILE} ")
    ENDFOREACH()   
    MESSAGE(STATUS "  Resources files: ${_TEMP_UIRC_STR}")
  ENDIF()  
  MESSAGE(STATUS "  Target: ${_WARE_TARGET}")
  
  SET(_QT_WARE_UIRC ${_OPNFLD_QT_WARE_UI} ${_OPNFLD_QT_WARE_RC})

  SET(_ALL_SRC_FILES "${CMAKE_BINARY_DIR}/${_OPNFLD_CPPSIGN_FILE}" ${_OPNFLD_SRC_FILES} ${_QT_WARE_UIRC})

  IF(MINGW)
    # workaround for CMake bug with MinGW
    ADD_LIBRARY(${_WARE_TARGET} SHARED ${_ALL_SRC_FILES})
  ELSE()
    ADD_LIBRARY(${_WARE_TARGET} MODULE ${_ALL_SRC_FILES})
  ENDIF()


  # ================ 
  # fragments detection 
  # ================

  SET(_OPNFLD_FRAGMENTS_PATH @OPENFLUID_FRAGMENTS_PATH@)
  SET(_OPNFLD_WARESDEV_PATH @OPENFLUID_WARESDEV_PATH@)
  SET(_OPNFLD_SHARED_PATH @OPENFLUID_USERSHARED_PATH@)

  # Adding directory of fragments inside the ware src
  INCLUDE_DIRECTORIES("${_OPNFLD_FRAGMENTS_PATH}")

  # Adding directory of fragments at workspace level
  IF(OPENFLUID_CURRENT_WORKSPACE_PATH)
    MESSAGE(STATUS "Found workspace path: ${OPENFLUID_CURRENT_WORKSPACE_PATH}")
    INCLUDE_DIRECTORIES("${OPENFLUID_CURRENT_WORKSPACE_PATH}/${_OPNFLD_WARESDEV_PATH}/${_OPNFLD_FRAGMENTS_PATH}")
  ENDIF()

  # Adding directory of fragments at userdata level
  INCLUDE_DIRECTORIES("${_OPNFLD_USERDATA_PATH}/${_OPNFLD_SHARED_PATH}/${_OPNFLD_FRAGMENTS_PATH}")


  SET_TARGET_PROPERTIES(${_WARE_TARGET} PROPERTIES PREFIX "" SUFFIX ${CMAKE_SHARED_LIBRARY_SUFFIX})
  SET_TARGET_PROPERTIES(${_WARE_TARGET} PROPERTIES OUTPUT_NAME ${WARE_ID}${_OPNFLD_WARE_FILENAME_SUFFIX})


  TARGET_INCLUDE_DIRECTORIES(${_WARE_TARGET}
                             PRIVATE ${OpenFLUID_INCLUDE_DIRS} ${WARE_INCLUDE_DIRS} ${CMAKE_CURRENT_BINARY_DIR})
  TARGET_LINK_DIRECTORIES(${_WARE_TARGET}
                          PRIVATE ${OpenFLUID_LIBRARY_DIRS} ${WARE_LIBRARY_DIRS} ${CMAKE_CURRENT_BINARY_DIR})

  IF(_OPNFLD_QT_REQUIRED)
    SET_TARGET_PROPERTIES(${_WARE_TARGET} PROPERTIES AUTOMOC ON)
    TARGET_LINK_LIBRARIES(${_WARE_TARGET} PUBLIC Qt5::Core Qt5::Widgets)
  ENDIF()

  TARGET_LINK_LIBRARIES(${_WARE_TARGET}
                        PRIVATE ${OpenFLUID_LIBRARIES} ${WARE_LINK_LIBS})

  IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE "Debug")
  ENDIF()
  STRING(TOUPPER ${CMAKE_BUILD_TYPE} _WAREBUILD_BUILD_TYPE)

  STRING(REPLACE ";" " " _WAREBUILD_CXX_FLAGS "${CMAKE_CXX_FLAGS};${CMAKE_CXX_FLAGS_${_WAREBUILD_BUILD_TYPE}}")
  TARGET_COMPILE_DEFINITIONS(${_WARE_TARGET} PRIVATE 
                             WAREBUILD_BUILD_TYPE="${_WAREBUILD_BUILD_TYPE}"
                             WAREBUILD_COMPILER_ID="${CMAKE_CXX_COMPILER_ID}"
                             WAREBUILD_COMPILER_VERSION="${CMAKE_CXX_COMPILER_VERSION}"
                             WAREBUILD_COMPILATION_FLAGS="${_WAREBUILD_CXX_FLAGS}")

  TARGET_COMPILE_DEFINITIONS(${_WARE_TARGET} PRIVATE 
                             WARE_LINKUID="${WARE_LINKUID}" 
                             WARE_ID="${WARE_ID}")
 

  MESSAGE(STATUS "  Install path: ${_OPNFLD_INSTALL_PATH}")

  INSTALL(TARGETS ${_WARE_TARGET} DESTINATION "${_OPNFLD_INSTALL_PATH}")


  # ================
  # i18n
  # ================


  IF(_BUILDARGS_I18N_LANGS)

    # ====== update ======
    
    FIND_PROGRAM(_LUPDATE_EXECUTABLE NAMES lupdate lupdate-qt4)

    IF(_LUPDATE_EXECUTABLE)

      ADD_CUSTOM_TARGET(update-translations-strings)

      FOREACH(_LANG ${_BUILDARGS_I18N_LANGS})
        
        SET(_LANG_TSFILE ${CMAKE_CURRENT_SOURCE_DIR}/${WARE_ID}-${_LANG}.ts)
      
        ADD_CUSTOM_TARGET(update-translation-strings-${_LANG}
                          COMMENT "Updating ${_LANG} translation strings for ${${_WARENAME}_ID}"
                          COMMAND ${_LUPDATE_EXECUTABLE} 
                                  "${CMAKE_CURRENT_SOURCE_DIR}" 
                                  "-I" "${CMAKE_CURRENT__SOURCE_DIR}"
                                  "-ts" ${_LANG_TSFILE})
     
        ADD_DEPENDENCIES(update-translations-strings update-translation-strings-${_LANG})
    
      ENDFOREACH()  
    ELSE()
      MESSAGE(STATUS "  Unable to add target for translations (lupdate not found)")
    ENDIF()
  
  
    # ====== release ======
  
    FIND_PROGRAM(_LRELEASE_EXECUTABLE NAMES lrelease lrelease-qt5)

    IF(_LRELEASE_EXECUTABLE)

      ADD_CUSTOM_TARGET(release-translations ALL)    
        
      SET (_ALL_LANGS)

      FOREACH(_LANG ${_BUILDARGS_I18N_LANGS})
  
        SET(_LANG_TSFILE ${CMAKE_CURRENT_SOURCE_DIR}/${WARE_ID}-${_LANG}.ts)
        SET(_LANG_QMFILE ${CMAKE_CURRENT_BINARY_DIR}/${WARE_ID}${_OPNFLD_WARE_FILENAME_SUFFIX}-${_LANG}.qm)
        
        ADD_CUSTOM_COMMAND(DEPENDS ${_LANG_TSFILE}
                           COMMAND ${_LRELEASE_EXECUTABLE} 
                                   ${_LANG_TSFILE}
                                   "-qm" ${_LANG_QMFILE} 
                            OUTPUT ${_LANG_QMFILE})
        
        ADD_CUSTOM_TARGET(release-translations-${_LANG}
                          COMMENT "Releasing ${_LANG} translation for ${WARE_ID}"
                          DEPENDS ${_LANG_QMFILE})

        ADD_DEPENDENCIES(release-translations release-translations-${_LANG})
        
        SET (_ALL_LANGS "${_ALL_LANGS}${_LANG} ")
        
        
        INSTALL(FILES "${_LANG_QMFILE}" DESTINATION "${_OPNFLD_INSTALL_PATH}")
          
      ENDFOREACH()
      
      MESSAGE(STATUS "  Registered translations: ${_ALL_LANGS}")

    ELSE()
      MESSAGE(STATUS "  Unable to register translations (lrelease not found)")    
    ENDIF()
  ENDIF()

ENDFUNCTION()


###########################################################################


# TOIMPL write doc (parameters, context variables if any, ...))
# WARE_DEFINITIONS : definitions to add at compile time
# WARE_INCLUDE_DIRS : directories to include headers
# WARE_LIBRARY_DIRS : directories for linking extra libraries 
# WARE_LINK_LIBS : extra libraries to link
FUNCTION(OPENFLUID_ADD_WAREPLUGIN) 
  SET(_ARGS_OPTIONS "")
  SET(_ARGS_ONEVAL "INSTALL_PATH;TARGET;WITH_PARAMSUI;PARAMSUI_INSTALL_PATH;PARAMSUI_TARGET")
  SET(_ARGS_MULTIVAL "CPP_FILES;FORTRAN_FILES;UI_FILES;RC_FILES;OPENFLUID_COMPONENTS;I18N_LANGS;PARAMSUI_CPP_FILES;PARAMSUI_UI_FILES;PARAMSUI_RC_FILES;PARAMSUI_OPENFLUID_COMPONENTS")
  CMAKE_PARSE_ARGUMENTS(_BUILDARGS "${_ARGS_OPTIONS}" "${_ARGS_ONEVAL}" "${_ARGS_MULTIVAL}" ${ARGN})

  # TOIMPL externalize the FIND_PACKAGE(OpenFLUID) calls
  # TOIMPL manage PARAMSUI_OPENFLUID_COMPONENTS
  # TOIMPL manage I18N_FILES_EXTRASCANS, maybe change name of variable
  # TOIMPL check management of I18N_LANGS

  _OPENFLUID_WAREPLUGIN_BUILD()

  IF(_BUILDARGS_WITH_PARAMSUI AND _BUILDARGS_PARAMSUI_CPP_FILES)
    SET(_BUILDARGS_CPP_FILES ${_BUILDARGS_PARAMSUI_CPP_FILES})
    SET(_BUILDARGS_FORTRAN_FILES)
    SET(_BUILDARGS_UI_FILES ${_BUILDARGS_PARAMSUI_UI_FILES})
    SET(_BUILDARGS_RC_FILES ${_BUILDARGS_PARAMSUI_RC_FILES})

    SET(_FORCE_PARAMSUI TRUE) # HACK see if better way to manage build of parameterization UI

    _OPENFLUID_WAREPLUGIN_BUILD()
  
  ENDIF()
ENDFUNCTION()


###########################################################################


# TOIMPL write doc
FUNCTION(OPENFLUID_ADD_WARETESTS)
  SET(_ARGS_OPTIONS "DISCOVER")
  SET(_ARGS_ONEVAL)
  SET(_ARGS_MULTIVAL "LIST")
  CMAKE_PARSE_ARGUMENTS(_TESTSARGS "${_ARGS_OPTIONS}" "${_ARGS_ONEVAL}" "${_ARGS_MULTIVAL}" ${ARGN})

  SET(_DATASETS)

  IF(_TESTSARGS_DISCOVER) # discover tests datasets (*.IN)
    FILE(GLOB _DATASETS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "*.IN")
  ELSE() # use provided list
    FOREACH(_DS ${_TESTSARGS_LIST})
      SET(_DATASETS ${_DATASETS} "${_DS}.IN")
    ENDFOREACH()
  ENDIF()

  SET(_TESTS_OUTPUT_DIR ${CMAKE_BINARY_DIR}/tests-output)  
  FILE(MAKE_DIRECTORY "${_TESTS_OUTPUT_DIR}")

  IF(_DATASETS)
    MESSAGE(STATUS "Adding simulation tests")
  ENDIF()
  FOREACH(_CURRTEST ${_DATASETS})
    GET_FILENAME_COMPONENT(_TESTNAME ${_CURRTEST} NAME)
    ADD_TEST(NAME ${_CURRTEST} 
            COMMAND "${OpenFLUID_CMD_PROGRAM}" "run"
                    "${CMAKE_CURRENT_SOURCE_DIR}/${_CURRTEST}"
                    "${_TESTS_OUTPUT_DIR}/${_TESTNAME}.OUT"
                    "-p" "${CMAKE_BINARY_DIR}"
                    "-n" "${CMAKE_BINARY_DIR}")
    MESSAGE(STATUS "  ${_CURRTEST}")
  ENDFOREACH()

ENDFUNCTION()  


###########################################################################


# TOIMPL write doc
FUNCTION(OPENFLUID_ADD_WAREDOC)
  SET(_ARGS_OPTIONS)
  SET(_ARGS_ONEVAL)
  SET(_ARGS_MULTIVAL)
  CMAKE_PARSE_ARGUMENTS(_DOCARGS "${_ARGS_OPTIONS}" "${_ARGS_ONEVAL}" "${_ARGS_MULTIVAL}" ${ARGN})

  MESSAGE(WARNING "not implemented") # TOIMPL
ENDFUNCTION()

