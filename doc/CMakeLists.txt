

IF(DOXYGEN)

  CONFIGURE_FILE(HTML-Doxyfile.in HTML-Doxyfile)

  FILE(MAKE_DIRECTORY "${OFBUILD_DIST_MAIN_DOC_DIR}")

  CONFIGURE_FILE(${OFBUILD_DOC_CONTENTS_DIR}/openfluid.png ${OFBUILD_DIST_MAIN_DOC_DIR}/html/openfluid.png COPYONLY)
  CONFIGURE_FILE(${OFBUILD_DOC_CONTENTS_DIR}/API_stack.png ${OFBUILD_DIST_MAIN_DOC_DIR}/html/API_stack.png COPYONLY)
 
    
  ADD_CUSTOM_COMMAND(
    OUTPUT "${OFBUILD_DIST_MAIN_DOC_DIR}/html/index.html"
    DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/HTML-Doxyfile"
    COMMAND "${DOXYGEN}"
       ARGS "${CMAKE_CURRENT_BINARY_DIR}/HTML-Doxyfile")

  ADD_CUSTOM_TARGET(openfluid-doc-html ALL DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/HTML-Doxyfile"
    "${OFBUILD_DIST_MAIN_DOC_DIR}/html/index.html" VERBATIM)


  INSTALL(DIRECTORY "${OFBUILD_DIST_MAIN_DOC_DIR}/"
          DESTINATION "${OPENFLUID_MAIN_DOC_INSTALL_PATH}")
          
          
  IF(PDFLATEX_COMPILER)
    CONFIGURE_FILE(LaTeX-Doxyfile.in LaTeX-Doxyfile)
    
    STRING(REPLACE "~" "$\\sim$" OFBUILD_LATEX_VERSION_FULL "${OPENFLUID_VERSION_FULL}")

    FILE(MAKE_DIRECTORY "${OFBUILD_DOCBUILD_DIR}")
    FILE(COPY "${CMAKE_CURRENT_SOURCE_DIR}/contents/" 
       DESTINATION "${OFBUILD_DOCBUILD_DIR}/latex"
       PATTERN "*.png"
       PATTERN "*.pdf")
    FILE(COPY "${CMAKE_CURRENT_SOURCE_DIR}/layout/" 
       DESTINATION "${OFBUILD_DOCBUILD_DIR}/latex"
       PATTERN "*.sty")
   
    CONFIGURE_FILE(layout/main.tex.in "${OFBUILD_DOCBUILD_DIR}/latex/main.tex")
   
    ADD_CUSTOM_COMMAND(
      OUTPUT "${OFBUILD_DOCBUILD_DIR}/latex/main.pdf"
      DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/LaTeX-Doxyfile"
      COMMAND "${DOXYGEN}"
         ARGS "${CMAKE_CURRENT_BINARY_DIR}/LaTeX-Doxyfile"
      COMMAND ${CMAKE_COMMAND} -E chdir ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_COMMAND} 
              "-DDOC_BUILD_DIR=${OFBUILD_DOCBUILD_DIR}" "-DPDFLATEX_COMPILER=${PDFLATEX_COMPILER}"
              "-P" "BuildPDF.cmake"       
    )

    ADD_CUSTOM_COMMAND(    
      OUTPUT  "${OFBUILD_DIST_MAIN_DOC_DIR}/openfluid_manual_${OPENFLUID_VERSION_FULL}.pdf"
      DEPENDS "${OFBUILD_DOCBUILD_DIR}/latex/main.pdf"      
      COMMAND ${CMAKE_COMMAND} -E copy "${OFBUILD_DOCBUILD_DIR}/latex/main.pdf" 
                                       "${OFBUILD_DIST_MAIN_DOC_DIR}/openfluid_manual_${OPENFLUID_VERSION_FULL}.pdf"       
    )  


    ADD_CUSTOM_TARGET(openfluid-doc-pdf ALL DEPENDS "${OFBUILD_DIST_MAIN_DOC_DIR}/openfluid_manual_${OPENFLUID_VERSION_FULL}.pdf" VERBATIM)   

  ENDIF() 
    
ENDIF()

